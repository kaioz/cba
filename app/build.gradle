
apply plugin: 'com.android.application'


// Manifest version information!
def versionMajor = 0
def versionMinor = 0
def versionPatch = 1
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def notificationLog = false

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
def time = new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))

def isTravis = "true".equals(System.getenv("TRAVIS"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))


android {
    compileSdkVersion 21
    buildToolsVersion '22.0.1'
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !isTravis
    }

    signingConfigs {
        debug {
            storeFile file("../keys/debug.keystore")
        }
        release {
            storeFile file('../keys/release.jks')
            storePassword '123456'
            keyAlias 'cba'
            keyPassword '123456'
        }
    }

    defaultConfig {
        applicationId "com.cocosw.commonbank"
        minSdkVersion 9
        //noinspection OldTargetApi
        targetSdkVersion 20
        versionCode versionMajor * 100000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}-${time}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "Boolean", "notificationLog", "${notificationLog}"

    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
            signingConfig signingConfigs.debug
            // 2.3 workaround
            minifyEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            versionNameSuffix '-release'
            zipAlignEnabled true
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
        disable 'IconDensities','InvalidPackage'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}


repositories {
    jcenter()
    flatDir {
        dirs '../libs'
    }
}

dependencies {
    //noinspection GradleDependency
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.android.support:support-v4:22.1.1'
    compile(name:'framework-2.3.2', ext:'aar')
    debugCompile(name:'debug-0.6.3', ext:'aar')
    compile 'com.cocosw.accessory:utils:0.7.6'
    compile 'com.cocosw.accessory:views:0.9.2'
    compile 'com.cocosw.accessory:adapter:0.2.0'
    compile 'com.cocosw.accessory:connectivity:0.4.1'
    compile 'com.cocosw:query:0.2.2'
    compile 'com.cocosw:lifecycle:0.1.2@aar'
    compile 'com.cocosw:undobar:1.8.1@aar'
    compile 'com.squareup:otto:1.3.6'
    compile 'com.github.kevinsawicki:http-request:5.6'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:3.0.2'
    compile 'com.google.maps.android:android-maps-utils:0.3.4'
    compile 'com.google.android.gms:play-services-maps:7.3.0'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
}